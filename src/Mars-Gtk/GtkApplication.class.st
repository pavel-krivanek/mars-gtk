Class {
	#name : #GtkApplication,
	#superclass : #Object,
	#traits : 'TGtkLibrary',
	#classTraits : 'TGtkLibrary classTrait',
	#instVars : [
		'runLoop',
		'windowRegistry',
		'shutdownRegistry',
		'resourceManager'
	],
	#classVars : [
		'Current'
	],
	#pools : [
		'GtkTypes'
	],
	#category : #'Mars-Gtk-Base'
}

{ #category : #accessing }
GtkApplication class >> current [
	^ Current
]

{ #category : #accessing }
GtkApplication class >> ensureRunning [

	self isRunning ifTrue: [ ^ self ]. 
	self new run
]

{ #category : #'class initialization' }
GtkApplication class >> initialize [

	SessionManager default registerSystemClassNamed: self name
]

{ #category : #testing }
GtkApplication class >> isRunning [ 
	
	^ self current notNil 
		and: [ self current isRunning ]
]

{ #category : #private }
GtkApplication class >> reset [
	<script>

	Current ifNil: [ ^ self ].
	Current stop.
	Current := nil.
	self flag: #TODO. "I'm placing this here, not sure is the right place but for now it works"
	GtkDestroySignal reset.
]

{ #category : #'system startup' }
GtkApplication class >> shutDown: quitting [
	"ensure Gtk3 is stop when shutting down"

	Current ifNil: [ ^ self ].
	Current shutDown: quitting.
	"WARNING: Do not NIL Current! This is needed to keep track of the open windows (otherwise 
	 they will be closed/will crash/will put you in a world of suffering."
]

{ #category : #'system startup' }
GtkApplication class >> startUp: resuming [
	
	resuming ifTrue: [ 
		Current := nil.
		self flag: #TODO. "I'm placing this here, not sure is the right place but for now it works"
		GtkDestroySignal reset ].
	
	Current ifNil: [ ^ self ].
	Current startUp: resuming
]

{ #category : #'system startup' }
GtkApplication >> ensureRunLoop [
	| runLoopClass |
	
	runLoopClass := GtkRunLoop bestStrategyClass.
	(runLoop notNil 
		and: [ runLoop class = runLoopClass ])
		ifTrue: [ ^ self ].
		
	runLoopClass install.
	^ GtkRunLoop current
]

{ #category : #private }
GtkApplication >> initArgC: argc argV: argv [

	^ self ffiCall: #(void gtk_init ("int *"void *argc, "char ***"void *argv))
]

{ #category : #private }
GtkApplication >> initGtk3 [
	"Initialize Gtk3"

	self 
		initArgC: ExternalAddress null 
		argV: ExternalAddress null
]

{ #category : #initialization }
GtkApplication >> initialize [

	super initialize.
	windowRegistry := Set new.
	shutdownRegistry := Set new.
	runLoop := self ensureRunLoop
]

{ #category : #testing }
GtkApplication >> isRunning [ 

	^ runLoop isRunning
]

{ #category : #registry }
GtkApplication >> registerShutdown: anObject [
	"Some objects need special shutdown action *before* going out. 
	 (This is the case, for example, of the GtkFormView used in OSGtkFormRenderer).
	 You can subscribe those objects here, and they will be called *before* shutting down 
	 the run loop.
	 BEWARE: object needs to implement #prepareForShutdown methiod"

	shutdownRegistry add: anObject
]

{ #category : #registry }
GtkApplication >> registerWindow: aWindow [

	windowRegistry add: aWindow.
	GtkWindowRegisterSignal emitWidget: aWindow	
]

{ #category : #accessing }
GtkApplication >> resourceManager [

	^ GtkExternalResourceManager uniqueInstance
]

{ #category : #running }
GtkApplication >> run [
	Current ifNotNil: [ Current stop ].
	Current := nil.
	self start.
	Current := self.
	
]

{ #category : #'system startup' }
GtkApplication >> shutDown: quitting [
	
	self isRunning ifFalse: [ ^ self ].
	quitting ifTrue: [
		"process shutdown *before* shutting down runloop, because actions may need 
		 some gtk side actions"
		runLoop defer: [ 
			shutdownRegistry do: [ :each | each prepareToShutdown ] ] ].
	runLoop stop
]

{ #category : #'private running' }
GtkApplication >> start [ 
	self isRunning ifTrue: [ self stop ].

	self initGtk3.
	GtkApplicationStartSignal emit.
	runLoop start
]

{ #category : #'system startup' }
GtkApplication >> startUp: resuming [

	resuming ifTrue: [ 
		"This should not be reached anyway, since Current is niled on class side startUp"
		^ self ]. 
	runLoop start
]

{ #category : #'private running' }
GtkApplication >> stop [
	self isRunning ifFalse: [ ^ self ].
	runLoop stop
]

{ #category : #running }
GtkApplication >> suspendRunLoopWhile: aBlock [
	self stop.
	^ aBlock ensure: [ self start ]
]

{ #category : #registry }
GtkApplication >> unregisterWindow: aWindow [

	GtkWindowUnregisterSignal emitWidget: aWindow.
	windowRegistry 
		remove: aWindow 
		ifAbsent: [ ]
]

{ #category : #accessing }
GtkApplication >> windowByHandle: aHandle [ 
	| numberHandle |

	numberHandle := aHandle asInteger.
	^ windowRegistry 
		detect: [ :each | each getHandle asInteger = numberHandle ] 
		ifNone: [ nil ]
]
